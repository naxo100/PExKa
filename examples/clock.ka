
%compartment: 'cell' 1

%agent: mRNA(enc~PER1~PER2~CRY1~CRY2~REVERBa,loc~cyto~nuc)
%agent: PER(id~1~2,P1~u~p,P2~u~p,cry,cki,loc~cyto~nuc)
%agent: CRY(id~1~2,per,clk,loc~cyto~nuc)
%agent: REV_ERBa(rore,loc~cyto~nuc)
%agent: GENE(id~PER1~PER2~CRY1~CRY2~REVERBa,S1,S2,S3,S4,S5)
%agent: CKI(per,loc~cyto~nuc)
%agent: EBOX_CLK_BMAL1(cry,gene)
%agent: RORE(REV_ERBa,gene)


#Chapter 'Translation'
'tlpo' mRNA(enc~PER1,loc~cyto) -> mRNA(enc~PER1,loc~cyto),PER(id~1,loc~cyto,P1~u,P2~u,cry,cki) @ 10.0
'tlpt' mRNA(enc~PER2,loc~cyto) -> mRNA(enc~PER2,loc~cyto),PER(id~2,loc~cyto,P1~u,P2~u,cry,cki)@ 10.0
'tlro' mRNA(enc~CRY1,loc~cyto) -> mRNA(enc~CRY1,loc~cyto),CRY(id~1,loc~cyto,per,clk) @ 1.031
'tlrt' mRNA(enc~CRY2,loc~cyto) -> mRNA(enc~CRY2,loc~cyto),CRY(id~2,loc~cyto,per,clk) @ 1.031
'tlrv' mRNA(enc~REVERBa,loc~cyto) -> mRNA(enc~REVERBa,loc~cyto),REV_ERBa(loc~cyto,rore) @ 2.53

#Chapter 'Transcription'
'trPo' 	GENE(id~PER1,S1!1,S2!2,S3!3,S4!4,S5!5),EBOX_CLK_BMAL1(cry,gene!1),EBOX_CLK_BMAL1(cry,gene!2),EBOX_CLK_BMAL1(cry,gene!3),EBOX_CLK_BMAL1(cry,gene!4),EBOX_CLK_BMAL1(cry,gene!5) -> \
		GENE(id~PER1,S1!1,S2!2,S3!3,S4!4,S5!5),EBOX_CLK_BMAL1(cry,gene!1),EBOX_CLK_BMAL1(cry,gene!2),EBOX_CLK_BMAL1(cry,gene!3),EBOX_CLK_BMAL1(cry,gene!4),EBOX_CLK_BMAL1(cry,gene!5),mRNA(enc~PER1,loc~nuc) @ 9415.947
'trPt' 	GENE(id~PER2,S1!1,S2!2,S3!3,S4!4,S5!5),EBOX_CLK_BMAL1(cry,gene!1),EBOX_CLK_BMAL1(cry,gene!2),EBOX_CLK_BMAL1(cry,gene!3),EBOX_CLK_BMAL1(cry,gene!4),EBOX_CLK_BMAL1(cry,gene!5) -> \
		GENE(id~PER2,S1!1,S2!2,S3!3,S4!4,S5!5),EBOX_CLK_BMAL1(cry,gene!1),EBOX_CLK_BMAL1(cry,gene!2),EBOX_CLK_BMAL1(cry,gene!3),EBOX_CLK_BMAL1(cry,gene!4),EBOX_CLK_BMAL1(cry,gene!5),mRNA(enc~PER2,loc~nuc) @ 3601.244
'trRo'	GENE(id~CRY1,S1!1,S2!2,S3!3,S4!4,S5),EBOX_CLK_BMAL1(cry,gene!1),RORE(REV_ERBa,gene!2),RORE(REV_ERBa,gene!3),RORE(REV_ERBa,gene!4)->\
		GENE(id~CRY1,S1!1,S2!2,S3!3,S4!4,S5),EBOX_CLK_BMAL1(cry,gene!1),RORE(REV_ERBa,gene!2),RORE(REV_ERBa,gene!3),RORE(REV_ERBa,gene!4),mRNA(enc~CRY1,loc~nuc) @105.3084
'trRt' 	GENE(id~CRY2,S1!1,S2!2,S3!3,S4!4,S5),EBOX_CLK_BMAL1(cry,gene!1),RORE(REV_ERBa,gene!2),RORE(REV_ERBa,gene!3),RORE(REV_ERBa,gene!4)->\
		GENE(id~CRY2,S1!1,S2!2,S3!3,S4!4,S5),EBOX_CLK_BMAL1(cry,gene!1),RORE(REV_ERBa,gene!2),RORE(REV_ERBa,gene!3),RORE(REV_ERBa,gene!4),mRNA(enc~CRY2,loc~nuc) @89.3318
'trRv' 	GENE(id~REVERBa,S1!1,S2!2,S3!3,S4,S5),EBOX_CLK_BMAL1(cry,gene!1),EBOX_CLK_BMAL1(cry,gene!2),EBOX_CLK_BMAL1(cry,gene!3) -> \
		GENE(id~REVERBa,S1!1,S2!2,S3!3,S4,S5),EBOX_CLK_BMAL1(cry,gene!1),EBOX_CLK_BMAL1(cry,gene!2),EBOX_CLK_BMAL1(cry,gene!3),mRNA(enc~REVERBa,loc~nuc) @ 0.58

#Chapter 'Degradation'
'uRv' REV_ERBa() -> @ 16.25
'umPo' mRNA(enc~PER1,loc~cyto) -> @ 6.21
'umPt' mRNA(enc~PER2,loc~cyto) -> @ 0.38
'umRo' mRNA(enc~CRY1,loc~cyto) -> @ 0.30
'umRt' mRNA(enc~CRY2,loc~cyto) -> @ 0.30
'umRv' mRNA(enc~REVERBa,loc~cyto) -> @ 15.11
'up' PER(P1~p,cry) -> @ 3.39
'upu' PER(P1~u) -> @ 0.08
'uro' CRY(id~1,per) -> @ 0.44
'urt' CRY(id~2,per) -> @ 0.59

#Chapter 'Phosphorylation'
'hoo' CKI(per!1),PER(id~1,P1~u,P2~u,cki!1) -> CKI(per!1),PER(id~1,P1~p,P2~u,cki!1) @ 0.29
'hot' PER(id~2,P1~u,P2~u,cki!1),CKI(per!1) -> PER(id~2,P1~p,P2~u,cki!1),CKI(per!1) @ 0.09
'hto' PER(id~1,P1~p,P2~u,cki!1),CKI(per!1) -> PER(id~1,P1~p,P2~p,cki!1),CKI(per!1) @ 1.45

#Chapter 'Nuclear Import / Export'
'ne Ref1' PER(loc~nuc,cry,cki) -> PER(loc~cyto,cry,cki) @ 0.71
'ne Ref2' PER(loc~nuc,cki,cry!1),CRY(loc~nuc,per!1,clk) -> PER(loc~cyto,cki,cry!1),CRY(loc~cyto,per!1,clk) @ 0.71
'ne Ref3' PER(loc~nuc,cry,cki!1),CKI(per!1,loc~nuc) -> PER(loc~cyto,cry,cki!1),CKI(per!1,loc~cyto)@ 0.71
'ne Ref4' PER(loc~nuc,cki!2,cry!1),CKI(per!2,loc~nuc),CRY(per!1,loc~nuc,clk) -> \
	PER(loc~cyto,cki!1,cry!2),CKI(per!1,loc~cyto),CRY(per!2,loc~cyto,clk) @ 0.71
'neRv' REV_ERBa(loc~nuc,rore) -> REV_ERBa(loc~cyto,rore) @ 0.71
'tmc' mRNA(loc~nuc) -> mRNA(loc~cyto) @ 0.42
	
'nl Ref1' PER(P1~p,P2~u,loc~cyto,cry,cki) -> PER(P1~p,P2~u,loc~nuc,cry,cki) @ 2.31
'nl Ref2' PER(P1~p,P2~u,loc~cyto,cry!1,cki),CRY(per!1,loc~cyto) -> PER(P1~p,P2~u,loc~nuc,cry!1,cki),CRY(per!1,loc~nuc) @ 2.31
'nl Ref3' PER(P1~p,P2~u,loc~cyto,cki!1,cry),CKI(per!1,loc~cyto) -> PER(P1~p,P2~u,loc~nuc,cki!1,cry),CKI(per!1,loc~nuc) @ 2.31
'nl Ref4' PER(P1~p,P2~u,loc~cyto,cry!2,cki!1),CKI(per!1,loc~cyto),CRY(per!2,loc~cyto) -> \
	PER(P1~p,P2~u,loc~nuc,cry!1,cki!2),CKI(per!2,loc~nuc),CRY(per!1,loc~nuc) @ 2.31
	

'nlRv' REV_ERBa(loc~cyto) -> REV_ERBa(loc~nuc) @ 2.31

#Chapter 'Reactions'
'ac Ref1' PER(cki,loc~cyto),CKI(per,loc~cyto) -> PER(cki!1,loc~cyto),CKI(per!1,loc~cyto) @ 0.000348
'ac Ref2' CKI(per,loc~nuc),PER(cki,loc~nuc) -> CKI(per!1,loc~nuc),PER(cki!1,loc~nuc) @ 0.040517
'ar Ref1' CRY(per,loc~cyto),PER(cry,loc~cyto,P1~p) -> CRY(per!1,loc~cyto),PER(cry!1,loc~cyto,P1~p) @ 0.0002
'ar Ref2' CRY(per,loc~nuc),PER(cry,loc~nuc,P1~p) -> CRY(per!1,loc~nuc),PER(cry!1,loc~nuc,P1~p) @ 0.023276
'bin' CRY(clk,loc~nuc),EBOX_CLK_BMAL1(cry,gene!_) -> CRY(clk!2,loc~nuc),EBOX_CLK_BMAL1(cry!2,gene!_) @ 127.2862
'binRv' REV_ERBa(loc~nuc,rore),RORE(gene!_,REV_ERBa) -> REV_ERBa(loc~nuc,rore!1),RORE(gene!_,REV_ERBa!1) @ 0.11207
'dc' CKI(per!1),PER(cki!1) -> CKI(per),PER(cki) @ 5.09
'dr' PER(cry!1),CRY(per!1) -> PER(cry),CRY(per) @ 0.35
'unbin' CRY(clk!2,loc~nuc),EBOX_CLK_BMAL1(cry!2,gene!_) -> CRY(clk,loc~nuc),EBOX_CLK_BMAL1(cry,gene!_) @ 23.78
'unbinRv' REV_ERBa(rore!1,loc~nuc),RORE(REV_ERBa!1,gene!_) -> REV_ERBa(rore,loc~nuc),RORE(REV_ERBa,gene!_) @ 21.76

#Chapter 'Light'
'zg Ref1' GENE(id~PER1) -> GENE(id~PER1),mRNA(enc~PER1,loc~nuc) | 1:count @ 'zg Ref1 var'
'zg Ref2' GENE(id~PER2) -> GENE(id~PER2),mRNA(enc~PER2,loc~nuc) | 1:count @ 'zg Ref1 var'

%var: 'zg Ref1 var' 0.0
%var: 'zg Ref2 var' 0.0

%use: 'cell'
#Initial Conditions:
%init: 2 (GENE(id~PER1,S1!1,S2!2,S3!3,S4!4,S5!5),EBOX_CLK_BMAL1(gene!1,cry),EBOX_CLK_BMAL1(gene!2,cry),EBOX_CLK_BMAL1(gene!3,cry),EBOX_CLK_BMAL1(gene!4,cry),EBOX_CLK_BMAL1(gene!5,cry))
%init: 2 (GENE(id~PER2,S1!1,S2!2,S3!3,S4!4,S5!5),EBOX_CLK_BMAL1(gene!1,cry),EBOX_CLK_BMAL1(gene!2,cry),EBOX_CLK_BMAL1(gene!3,cry),EBOX_CLK_BMAL1(gene!4,cry),EBOX_CLK_BMAL1(gene!5,cry))
%init: 2 (GENE(id~CRY1,S1!1,S2!2,S3!3,S4!4,S5),EBOX_CLK_BMAL1(gene!1,cry),RORE(gene!2,REV_ERBa),RORE(gene!3,REV_ERBa),RORE(gene!4,REV_ERBa))
%init: 2 (GENE(id~CRY2,S1!1,S2!2,S3!3,S4!4,S5),EBOX_CLK_BMAL1(gene!1,cry),RORE(gene!2,REV_ERBa),RORE(gene!3,REV_ERBa),RORE(gene!4,REV_ERBa))
%init: 2 (GENE(id~REVERBa,S1!1,S2!2,S3!3,S4,S5),EBOX_CLK_BMAL1(gene!1,cry),EBOX_CLK_BMAL1(gene!2,cry),EBOX_CLK_BMAL1(gene!3,cry))
%init: 4 (mRNA(loc~cyto,enc~PER1))
%init: 23 (mRNA(loc~cyto,enc~PER2))
%init: 29 (mRNA(loc~nuc,enc~CRY1))
%init: 4706 (mRNA(loc~cyto,enc~CRY1))
%init: 24  (mRNA(loc~nuc,enc~CRY2))
%init: 3996 (mRNA(loc~cyto,enc~CRY2))
%init: 130  (PER(id~1,loc~cyto,cki,P1~u,cry,P2~u))
%init: 1374  (PER(id~2,loc~cyto,cki,P1~u,cry,P2~u))
%init: 74  (PER(id~1,loc~cyto,cki!1,P1~u,cry,P2~u),CKI(per!1,loc~cyto))
%init: 815  (PER(id~2,loc~cyto,cki!1,P1~u,cry,P2~u),CKI(per!1,loc~cyto))
%init: 11070  (CRY(id~1,per,loc~cyto,clk))
%init: 6993  (CRY(id~2,per,loc~cyto,clk))
%init: 10404  (CKI(per,loc~cyto))
%init: 4  (CKI(per,loc~nuc))
%init: 1  (PER(id~1,loc~cyto,cki!1,P1~p,cry,P2~u),CKI(per!1,loc~cyto))
%init: 7  (PER(id~2,loc~cyto,cki!1,P1~p,cry,P2~u),CKI(per!1,loc~cyto))
%init: 4  (PER(id~2,loc~cyto,cki,P1~p,cry,P2~u))
%init: 1  (PER(id~1,loc~cyto,cki!1,P1~p,cry,P2~p),CKI(per!1,loc~cyto))
%init: 1  (PER(id~1,loc~cyto,cki,P1~p,cry,P2~p))
%init: 1  (PER(id~1,loc~cyto,cki,P1~p,cry!1,P2~u),CRY(id~1,per!1,loc~cyto,clk))
%init: 14 (PER(id~2,loc~cyto,cki,P1~p,cry!1,P2~u),CRY(id~1,per!1,loc~cyto,clk))
%init: 1 (PER(id~1,loc~cyto,cki!2,P1~p,cry!1,P2~u),CRY(id~1,per!1,loc~cyto,clk),CKI(per!2,loc~cyto))
%init: 12 (PER(id~2,loc~cyto,cki!2,P1~p,cry!1,P2~u),CRY(id~1,per!1,loc~cyto,clk),CKI(per!2,loc~cyto))
%init: 1 (PER(id~1,loc~cyto,cki,P1~p,cry!1,P2~u),CRY(id~2,per!1,loc~cyto,clk))
%init: 9  (CRY(id~2,per!1,loc~cyto,clk),PER(id~2,loc~cyto,cki,P1~p,cry!1,P2~u))
%init: 1 (PER(id~1,loc~cyto,cki!2,P1~p,cry!1,P2~u),CRY(id~2,per!1,loc~cyto,clk),CKI(per!2,loc~cyto))
%init: 7 (PER(id~2,loc~cyto,cki!2,P1~p,cry!1,P2~u),CKI(per!2,loc~cyto),CRY(id~2,per!1,loc~cyto,clk))
%init: 17 (PER(id~1,loc~cyto,cki,P1~p,cry!1,P2~p),CRY(id~1,per!1,loc~cyto,clk))
%init: 11 (PER(id~1,loc~cyto,cki!1,P1~p,cry!2,P2~p),CRY(id~1,loc~cyto,clk,per!2),CKI(per!1,loc~cyto))
%init: 10 (PER(id~1,loc~cyto,cki,P1~p,cry!1,P2~p),CRY(id~2,per!1,loc~cyto,clk))
%init: 7 (PER(id~1,loc~cyto,cki!1,P1~p,cry!2,P2~p),CRY(id~2,loc~cyto,clk,per!2),CKI(per!1,loc~cyto))


%obs: 'nucleus:mRNA(enc~PER1)' mRNA(enc~PER1,loc~nuc)
%obs: 'nucleus:mRNA(enc~PER2)' mRNA(enc~PER2,loc~nuc)
%obs: 'nucleus:mRNA(enc~CRY1)' mRNA(enc~CRY1,loc~nuc)
%obs: 'nucleus:mRNA(enc~CRY2)' mRNA(enc~CRY2,loc~nuc)
%obs: 'nucleus:PER(id~1)' PER(id~1,loc~nuc)
%obs: 'nucleus:PER(id~2)' PER(id~2,loc~nuc)
%obs: 'nucleus:CRY(id~1)' CRY(id~1,loc~nuc)
%obs: 'nucleus:CRY(id~2)' CRY(id~2,loc~nuc)

%obs: 'cytosol:mRNA(enc~PER1)' mRNA(enc~PER1,loc~cyto)
%obs: 'cytosol:mRNA(enc~PER2)' mRNA(enc~PER2,loc~cyto)
%obs: 'cytosol:mRNA(enc~CRY1)' mRNA(enc~CRY1,loc~cyto)
%obs: 'cytosol:mRNA(enc~CRY2)' mRNA(enc~CRY2,loc~cyto)
%obs: 'cytosol:PER(id~1)' PER(id~1,loc~cyto)
%obs: 'cytosol:PER(id~2)' PER(id~2,loc~cyto)
%obs: 'cytosol:CRY(id~1)' CRY(id~1,loc~cyto)
%obs: 'cytosol:CRY(id~2)' CRY(id~2,loc~cyto)



%mod: repeat ([T] [mod] 12) = 0 do \
	$UPDATE 'zg Ref1 var' 0.000339;\
	$UPDATE 'zg Ref2 var' 0.000339\
until [false]

#%mod: repeat ([T] [mod] 24) = 0.0000001 do \
#	$UPDATE 'zg Ref1 var' 0.0;\
#	$UPDATE 'zg Ref2 var' 0.0\
#until [false]


%token: count

%init: count <- 0
%obs: 'count' |count|












#Perturbations:
#%var: 'N' 1
#%mod: repeat [T] > 12*'N' && 'N'[mod]2 = 0 do \
#	$UPDATE 'zg Ref1' 0.000339;\
#	$UPDATE 'zg Ref2' 0.000339;\
#	$UPDATE 'N' 'N'+1\
#until [false]
#%mod: repeat [T] > 12*'N' && 'N'[mod]2 = 1 do \
#	$UPDATE 'zg Ref1' 0.00;\
#	$UPDATE 'zg Ref2' 0.00;\
#	$UPDATE 'N' 'N'+1\
#until [false]

